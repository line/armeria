import com.linecorp.armeria.conventions.spring.SpringBuildUtil

dependencies {
    api libs.spring.boot2.actuator.autoconfigure
    api libs.javax.inject
    compileOnly libs.javax.validation

    optionalApi project(':prometheus1')
    optionalApi libs.micrometer.prometheus.legacy
    optionalApi libs.dropwizard.metrics.json

    implementation libs.spring.boot2.autoconfigure
    optionalImplementation libs.reactor.core

    testImplementation libs.spring.boot2.starter.actuator
    testImplementation libs.spring.boot2.starter.test
    testImplementation libs.micrometer.prometheus
    testImplementation project(':spring:boot2-starter')
}

def boot3Actuator = "boot3-actuator-autoconfigure"
def boot4Actuator = "boot4-actuator-autoconfigure"

SpringBuildUtil.copyTestSources(project(":spring:${boot4Actuator}"), project) {
    include "**"
    filesMatching("**/*.java") {
        filter { String line ->
            line.replaceAll(/\bjakarta\./, 'javax.')
        }
    }
    exclude "**/EnableTestMetrics.java"
    exclude "**/SettableHealthIndicator.java"
}

SpringBuildUtil.useMainSources(project(":spring:${boot4Actuator}"), project) {
    exclude "**/HealthConfiguration.java"
    exclude "**/package-info.java"
}

SpringBuildUtil.useMainSources(project(":spring:${boot3Actuator}"), project)
