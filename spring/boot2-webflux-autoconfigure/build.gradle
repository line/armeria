configurations {
    // TODO(hyangtack) The followings are not transitive dependencies and they are not in testRuntime
    //                 dependencies. Need to figure out why they are included in shadedTestRuntime dependencies.
    // Exclude jetty from shadedTest.
    shadedTestRuntime.exclude group: 'org.eclipse.jetty'
    shadedTestRuntime.exclude group: 'org.eclipse.jetty.http2'
}

dependencies {
    // To let a user choose between thrift and thrift0.9.
    compileOnly project(':thrift0.13')
    implementation project(':logback')

    optionalApi 'io.micrometer:micrometer-registry-prometheus'
    optionalApi 'io.dropwizard.metrics:metrics-json'
    api 'javax.inject:javax.inject'
    compileOnly 'javax.validation:validation-api'
    api 'org.springframework.boot:spring-boot-starter-webflux'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation project(':spring:boot2-actuator-autoconfigure')
    testImplementation project(':spring:boot2-autoconfigure')
    testImplementation project(':thrift0.13')
    testImplementation "io.projectreactor:reactor-test"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// Copy common files from boot-autoconfigure module to gen-src directory in order to use them as a source set.
task generateSources(type: Copy) {
    from "${rootProject.projectDir}/spring/boot2-autoconfigure/src/main/java"
    into "${project.ext.genSrcDir}/main/java"

    // Copy from /internal/spring package.
    include '**/ArmeriaConfigurationNetUtil.java'
    include '**/ArmeriaConfigurationUtil.java'
    include '**/CustomAlias*KeyManager*.java'
    include '**/DropwizardSupport.java'
    include '**/PrometheusSupport.java'
    include '**/ThriftServiceUtils.java'

    // Copy from /spring package.
    include '**/*ArmeriaBeanPostProcessor*.java'
    include '**/*ExampleHeaders.java'
    include '**/*ExampleRequest.java'
    include '**/*RegistrationBean.java'
    include '**/ArmeriaServerConfigurator.java'
    include '**/ArmeriaSettings.java'
    include '**/DocServiceConfigurator.java'
    include '**/LocalArmeriaPort.java'
    include '**/LocalArmeriaPorts.java'
    include '**/*MeterIdPrefixFunctionFactory.java'
    include '**/Ssl.java'
}

tasks.compileJava.dependsOn(generateSources)
