dependencies {
    api libs.spring.boot2.actuator.autoconfigure
    api libs.javax.inject
    compileOnly libs.javax.validation

    optionalApi project(':prometheus1')
    optionalApi libs.micrometer.prometheus.legacy
    optionalApi libs.dropwizard.metrics.json

    implementation libs.spring.boot2.autoconfigure
    optionalImplementation libs.reactor.core

    testImplementation libs.spring.boot2.starter.actuator
    testImplementation libs.spring.boot2.starter.test
    testImplementation libs.micrometer.prometheus
    testImplementation project(':spring:boot2-starter')
}

// Use the sources from ':spring:boot3-actuator-autoconfigure'.
def boot3Actuator = "boot3-actuator-autoconfigure"
def boot3ActuatorProjectDir = "${rootProject.projectDir}/spring/$boot3Actuator"

tasks.register('generateTestSources', Copy.class) {
    from "${boot3ActuatorProjectDir}/src/test/java"
    into "${project.ext.genSrcDir}/test/java"

    exclude "**/EnableTestMetrics.java"
}

tasks.compileJava.dependsOn(project(":spring:$boot3Actuator").tasks.compileJava)
tasks.compileTestJava.dependsOn(project(":spring:$boot3Actuator").tasks.compileTestJava)
tasks.compileTestJava.dependsOn(tasks.generateTestSources)
tasks.sourcesJar.dependsOn(project(":spring:$boot3Actuator").tasks.sourcesJar)

tasks.compileJava.source "${boot3ActuatorProjectDir}/src/main/java",
        "${boot3ActuatorProjectDir}/gen-src/main/java"
tasks.compileTestJava.source "${boot3ActuatorProjectDir}/gen-src/test/java"
tasks.processTestResources.from "${boot3ActuatorProjectDir}/src/test/resources"
tasks.sourcesJar.from "${boot3ActuatorProjectDir}/src/main/java", "${boot3ActuatorProjectDir}/gen-src/main/java"
tasks.sourcesJar.from "${boot3ActuatorProjectDir}/src/main/resources"
tasks.javadoc.source "${boot3ActuatorProjectDir}/src/main/java", "${boot3ActuatorProjectDir}/gen-src/main/java"
