final def SPRING_BOOT_VERSION = '2.1.18.RELEASE'
final def SPRING_VERSION = '5.1.20.RELEASE'

dependencies {
    implementation(project(':spring:boot3-starter')) {
        exclude group: 'org.springframework.boot', module: 'spring-boot-autoconfigure'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter'
    }
    implementation project(':tomcat8')

    ['spring-boot-starter', 'spring-boot-autoconfigure', 'spring-boot-starter-web'].each {
        implementation("org.springframework.boot:$it") {
            version {
                // Will fail the build if the override doesn't work
                strictly SPRING_BOOT_VERSION
            }
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
        }
    }

    [ 'spring-beans', 'spring-web' ].each {
        implementation("org.springframework:$it") {
            version {
                // Will fail the build if the override doesn't work
                strictly SPRING_VERSION
            }
        }
    }

    implementation libs.tomcat8.core
    implementation libs.tomcat8.jasper
    implementation libs.tomcat8.el

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        version {
            // Will fail the build if the override doesn't work
            strictly SPRING_BOOT_VERSION
        }
    }
}

// Use the sources from ':it:spring:boot3-tomcat9'.
// NB: We should never add these directories using the 'sourceSets' directive because that will make
//     them added to more than one project and having a source directory with more than one output directory
//     will confuse IDEs such as IntelliJ IDEA.
def tomcatProjectDir = "${rootProject.projectDir}/it/spring/boot3-tomcat9"
tasks.compileJava.source "${tomcatProjectDir}/src/main/java"
tasks.processResources.from "${tomcatProjectDir}/src/main/resources"
tasks.compileTestJava.source "${tomcatProjectDir}/src/test/java/com/linecorp/armeria/spring/tomcat/demo/ErrorHandlingTest",
        "${tomcatProjectDir}/src/test/java/com/linecorp/armeria/spring/tomcat/demo/SpringTomcatApplicationItTest",
        "${tomcatProjectDir}/gen-src/test/java"
tasks.processTestResources.from "${tomcatProjectDir}/src/test/resources"
tasks.sourcesJar.from "${tomcatProjectDir}/src/main/java"
tasks.sourcesJar.from "${tomcatProjectDir}/src/main/resources"
tasks.javadoc.source "${tomcatProjectDir}/src/main/java"

// Disable checkstyle because it's checked by ':it:spring:boot2-tomcat9'.
tasks.withType(Checkstyle) {
    onlyIf { false }
}
