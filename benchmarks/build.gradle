buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "me.champeau.gradle:jmh-gradle-plugin:${managedVersions['me.champeau.gradle:jmh-gradle-plugin']}"
    }
}

apply plugin: 'me.champeau.gradle.jmh'

def jmhInclude = rootProject.findProperty('jmh.include') ?: ''

dependencies {
    compile 'io.grpc:grpc-okhttp'
    compile 'io.grpc:grpc-netty-shaded'
}

dependencies {
    compile project(':grpc')
    compile project(':retrofit2')
    compile project(':thrift')

    compile 'com.squareup.retrofit2:adapter-java8'
    compile 'com.squareup.retrofit2:converter-jackson'
}

jmh {
    if (jmhInclude) {
        include = jmhInclude
    }

    if (rootProject.hasProperty('jmh.forceGC')) {
        forceGC = 'true'.equals(rootProject.findProperty('jmh.forceGC'))
    } else {
        forceGC = true
    }


    duplicateClassesStrategy = DuplicatesStrategy.EXCLUDE
    jmhVersion = managedVersions['org.openjdk.jmh:jmh-core']

    if (rootProject.hasProperty('jmh.params')) {
        benchmarkParameters = [:]
        rootProject.findProperty('jmh.params').split(';').each {
            def parts = it.split('=')
            benchmarkParameters[parts[0]] = parts[1].tokenize(',')
        }
    }

    if (rootProject.hasProperty('jmh.fork')) {
        fork = Integer.parseInt(String.valueOf(rootProject.findProperty('jmh.fork')))
    } else {
        fork = 1
    }
    if (rootProject.hasProperty('jmh.iterations')) {
        iterations = Integer.parseInt(String.valueOf(rootProject.findProperty('jmh.iterations')))
    }
    if (rootProject.hasProperty('jmh.warmupIterations')) {
        warmupIterations = Integer.parseInt(String.valueOf(rootProject.findProperty('jmh.warmupIterations')))
    } else {
        warmupIterations = iterations
    }
    if (rootProject.hasProperty('jmh.profilers')) {
        profilers = String.valueOf(rootProject.findProperty('jmh.profilers')).split(',')
    }
    if (rootProject.hasProperty('jmh.threads')) {
        threads = Integer.parseInt(String.valueOf(rootProject.findProperty('jmh.threads')))
    }
    if (rootProject.hasProperty('jmh.verbose')) {
        verbosity = 'EXTRA'
    }
}
