dependencies {
    implementation 'org.scala-lang.modules:scala-java8-compat_3'

    // Added for supporting Scala types in Jackson{Request,Response}ConverterFunction
    implementation 'com.fasterxml.jackson.module:jackson-module-scala_3'
}

// Use the sources from ':scala_2.13'.
// NB: We should never add these directories using the 'sourceSets' directive because that will make
//     them added to more than one project and having a source directory with more than one output directory
//     will confuse IDEs such as IntelliJ IDEA.
def scala213ProjectDir = "${rootProject.projectDir}/scala/scala_2.13"
tasks.compileScala.source "${scala213ProjectDir}/src/main/scala", "${scala213ProjectDir}/src/main/scala_2.13"
tasks.processResources.from "${scala213ProjectDir}/src/main/resources"

tasks.compileTestScala.source "${scala213ProjectDir}/src/test/scala"
tasks.processTestResources.from "${scala213ProjectDir}/src/test/resources"

tasks.sourcesJar.from "${scala213ProjectDir}/src/main/scala"
tasks.sourcesJar.from "${scala213ProjectDir}/src/main/resources"
tasks.scaladoc.source "${scala213ProjectDir}/src/main/scala"
