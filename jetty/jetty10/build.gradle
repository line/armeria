dependencies {
    api libs.jetty10.server

    // Can't exclude slf4j 1.x because the core module uses it as an api configuration.
    // If this becomes problem, we will refactor the core more and exclude the slf4j 1.x dependency.
    implementation libs.slf4j2.api

    testImplementation libs.asm
    testImplementation libs.jetty10.annotations
    testImplementation libs.jetty10.apache.jsp
    testImplementation libs.jetty10.apache.jstl
    testImplementation libs.jetty10.webapp
    testImplementation libs.logback14
}


// Use the sources from ':jetty11'.
def jetty11ProjectDir = "${rootProject.projectDir}/jetty/jetty11"

// Copy common files from jetty11 module to gen-src directory in order to use them as a source set.
tasks.register('generateSources', Copy.class) {
    from "${jetty11ProjectDir}/src/main/java"
    into "${project.ext.genSrcDir}/main/java"
    exclude '**/DispatcherTypeUtil.java'
    exclude '**/server/jetty/package-info.java'
}

tasks.register('generateTestSources', Copy.class) {
    from "${jetty11ProjectDir}/src/test/java"
    into "${project.ext.genSrcDir}/test/java"
    exclude '**/AsyncStreamingHandlerFunction.java'
    exclude '**/JettyServiceTestUtil.java'
}

tasks.generateSources.dependsOn(generateTestSources)
tasks.compileJava.dependsOn(generateSources)
tasks.compileTestJava.dependsOn(generateSources)

tasks.processTestResources.from "${jetty11ProjectDir}/src/test/resources"
