// This module builds and publishes 'armeria-thrift0.9', which is compiled with
// the source code of ':thrift' and libthrift 0.9.
//
// The reason we have to do this is because libthrift kept API compatibility but
// not ABI compatibility between 0.9 and 0.10, i.e. we can use the same source
// but not the same class files.
//
// See also: ../thrift0.11/build.gradle

final def THRIFT_VERSION = '0.9.3-1'

dependencyManagement {
    dependencies {
        dependency("org.apache.thrift:libthrift:${THRIFT_VERSION}") {
            exclude group: 'javax.annotation',          name: 'javax.annotation-api'
            exclude group: 'org.apache.httpcomponents', name: 'httpcore'
            exclude group: 'org.apache.httpcomponents', name: 'httpclient'
        }
    }
}

dependencies {
    // Thrift 0.9
    compile 'org.apache.thrift:libthrift'
    compile 'jakarta.annotation:jakarta.annotation-api'

    // Jetty, for testing TServlet interoperability.
    testCompile 'org.eclipse.jetty:jetty-webapp'
    testCompile 'org.eclipse.jetty.http2:http2-server'

    // Dropwizard and Prometheus, for testing metrics integration
    testCompile 'io.dropwizard.metrics:metrics-core'
    testCompile 'io.prometheus:simpleclient_common'
}

// Use the sources from ':thrift'.
// NB: We should never add these directories using the 'sourceSets' directive because that will make
//     them added to more than one project and having a source directory with more than one output directory
//     will confuse IDEs such as IntelliJ IDEA.
tasks.compileJava.source "${rootProject.projectDir}/thrift/src/main/java"
tasks.processResources.from "${rootProject.projectDir}/thrift/src/main/resources"
tasks.compileTestJava.source "${rootProject.projectDir}/thrift/src/test/java"
tasks.processTestResources.from "${rootProject.projectDir}/thrift/src/test/resources"
tasks.sourceJar.from "${rootProject.projectDir}/thrift/src/main/java"
tasks.sourceJar.from "${rootProject.projectDir}/thrift/src/main/resources"
tasks.javadoc.source "${rootProject.projectDir}/thrift/src/main/java"

// Use the old compiler.
ext {
    thriftVersion = THRIFT_VERSION.substring(0, THRIFT_VERSION.lastIndexOf('.'));
    disableThriftJson()
}

// Keep the original Guava references in ThriftListenableFuture,
// which is the only place we expose Guava classes in our public API.
// NB: Keep this same with ':thrift'.
tasks.shadedJar.exclude 'com/linecorp/armeria/common/thrift/ThriftListenableFuture*'
tasks.shadedJar.doLast {
    ant.jar(update: true, destfile: tasks.shadedJar.archivePath) {
        sourceSets.main.output.classesDirs.each { classesDir ->
            fileset(dir: "$classesDir",
                    includes: 'com/linecorp/armeria/common/thrift/ThriftListenableFuture*')
        }
    }
}

// Disable checkstyle because it's checked by ':thrift'.
tasks.withType(Checkstyle) {
    onlyIf { false }
}

// Make sure the correct Thrift versions are used.
afterEvaluate {
    [configurations.runtime, configurations.testRuntime].each { cfg ->
        cfg.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            def group = artifact.moduleVersion.id.group
            def version = artifact.moduleVersion.id.version
            switch (group) {
                case 'org.apache.thrift':
                    assert version == THRIFT_VERSION
                    break
            }
        }
    }
}
