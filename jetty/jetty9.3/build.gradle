dependencies {
    // Use the classes compiled with the latest Jetty,
    // but use an older version for test classes.
    testImplementation(project(':jetty9')) {
        exclude group: 'org.eclipse.jetty', module: 'jetty-server'
    }

    testImplementation libs.jetty93.server
    testImplementation libs.jetty93.webapp
    testImplementation libs.jetty93.annotations
    testImplementation libs.jetty93.apache.jsp
    testImplementation libs.jetty93.apache.jstl

    testImplementation("org.eclipse.jdt:ecj") {
        version {
            // As a workaround for JSP compiler error on Java 17, forcibly specify the newer version
            strictly "3.24.0"
        }
    }
}


tasks.register("copyJetty9Test", Copy.class) {
    from("${rootProject.projectDir}/jetty/jetty9/src/test")
    into("${project.ext.genSrcDir}/test")
}
tasks.compileTestJava.dependsOn("copyJetty9Test")
tasks.processTestResources.dependsOn("copyJetty9Test")

// Disable checkstyle because it's checked by ':jetty9'.
tasks.withType(Checkstyle) {
    onlyIf { false }
}
