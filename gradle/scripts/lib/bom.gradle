configure(projectsWithFlags('bom')) {
    apply plugin: 'java-platform'
    apply plugin: 'maven-publish'

    publishing {
        publications {
            bom(MavenPublication) {
                from components.javaPlatform

                pom.packaging = 'pom'
            }
        }
    }

    dependencies {
        constraints {
            projectsWithFlags('java', 'publish').toList().sort { a, b ->
                def groupComparison = "${a.group}".compareTo("${b.group}")
                if (groupComparison != 0) {
                    return groupComparison
                }
                return "${a.ext.artifactId}".compareTo("${b.ext.artifactId}")
            }.each { p ->
                api "${p.group}:${p.ext.artifactId}:${p.version}"
            }
        }
    }

    tasks.assemble.dependsOn {
        tasks.generatePomFileForBomPublication
    }
}
