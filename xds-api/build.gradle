dependencies {
    configurations.configureEach {
        resolutionStrategy {
            force "com.google.protobuf:protobuf-java:${libs.versions.protobuf.asProvider().get()}"
        }
    }

    api project(":xds-validator")

    compileOnly libs.protobuf.java
    compileOnly libs.protobuf.java.util
    compileOnly project(":grpc")
    compileOnly libs.protoc.gen.validate

    // shade to avoid Validate.class compiled by proto 4 leaking in
    // `xds-api`'s Validate.class wins since
    // 1) from(main) is called duration configuration phase
    // 2) dependencies are included in ShadowJar's copy spec during execution phase
    // 3) the default DuplicatesStrategy is EXCLUDE for ShadowJar
    shade (libs.protoc.pgv.java.stub) {
        exclude group: 'com.google.protobuf'
    }

    testImplementation libs.protobuf.java
    testImplementation libs.protobuf.java.util
    testImplementation libs.protoc.pgv.java.stub
    testImplementation libs.re2j
}

tasks.withType(JavaCompile).configureEach {
    options.forkOptions.memoryMaximumSize = '1g'
}

tasks.withType(Javadoc).configureEach {
    enabled = false
}
