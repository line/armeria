configure(projectsWithFlags('publish', 'java')) {
    publishing {
        publications {
            jar(MavenPublication) {
                from components.java

                // We don't publish Gradle metadata yet so don't need to worry about these variants.
                suppressPomMetadataWarningsFor('optionalApiElements')
                suppressPomMetadataWarningsFor('optionalRuntimeElements')

                // Publish resolved versions
                versionMapping {
                    allVariants {
                        fromResolutionResult()
                    }
                }

                // Replace published jar with shaded when available. We manually create shading tasks instead of
                // applying the shadow plugin itself so need to do it ourselves.
                def jarOverrideFile = null
                def jarOverrideTask = null
                if (tasks.findByName('trimShadedJar')) {
                    jarOverrideFile = tasks.trimShadedJar.outJarFiles.find() as File
                    jarOverrideTask = tasks.trimShadedJar
                } else if (tasks.findByName('shadedJar')) {
                    jarOverrideFile = tasks.shadedJar.archivePath
                    jarOverrideTask = tasks.shadedJar
                }
                if (jarOverrideFile != null) {
                    // For some reason this needs to be in afterEvaluate or dependencies are lost from the POM.
                    afterEvaluate {
                        def unshaded = artifacts.find { it.classifier == null && it.extension == 'jar' }
                        artifacts.remove(unshaded)
                        artifact(jarOverrideFile).builtBy(jarOverrideTask)
                    }
                }

                // Clean up the POM.
                pom {
                    withXml {
                        def rootNode = asNode()

                        // Do not import 'com.linecorp.armeria:dependencyManagement'.
                        if (rootNode.get('dependencyManagement')) {
                            def dependencyManagement = rootNode.get('dependencyManagement')[0]
                            if (dependencyManagement.get('dependencies')) {
                                def dependencies = dependencyManagement.get('dependencies')[0]
                                dependencies.findAll {
                                    def groupId = it.get('groupId')[0]
                                    def artifactId = it.get('artifactId')[0]
                                    groupId == rootProject.group && artifactId == 'dependencyManagement'
                                }.each {
                                    dependencies.remove(it)
                                }

                                // Clean up empty tags.
                                if (dependencies.directChildren.isEmpty()) {
                                    dependencyManagement.remove(dependencies)
                                    if (dependencyManagement.directChildren.isEmpty()) {
                                        rootNode.remove(dependencyManagement)
                                    }
                                }
                            }
                        }

                        // Strip out shaded dependencies. We manually create shading tasks instead of applying
                        // the shadow plugin itself so need to do it ourselves.
                        if (rootNode.get('dependencies')) {
                            def dependencies = rootNode.get('dependencies')[0]
                            def shaded = dependencies.findAll {
                                def groupId = it.get('groupId')[0]
                                def artifactId = it.get('artifactId')[0]
                                return project.ext.relocations.find {
                                    it.name == "${groupId.text()}:${artifactId.text()}"
                                }
                            }
                            shaded.each { dependencies.remove(it) }
                        }
                    }
                }
            }
        }
    }
}

configure(projectsWithFlags('publish')) {
    publishing {
        publications.each { publication ->
            // Shared configuration for all publications, including BOM
            def currentArtifactId = project.ext.artifactId
            publication.artifactId currentArtifactId

            // Generate the POM.
            publication.pom {
                // Write the elements required by OSSRH.
                name = "${project.ext.projectName} (${currentArtifactId})"
                description = "${project.ext.projectDescription} (${currentArtifactId})"
                url = "${project.ext.projectUrl}"
                inceptionYear = "${project.ext.inceptionYear}"

                licenses {
                    license {
                        name = "${project.ext.licenseName}"
                        url = "${project.ext.licenseUrl}"
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        name = "${project.ext.authorName}"
                        email = "${project.ext.authorEmail}"
                        url = "${project.ext.authorUrl}"
                    }
                }

                scm {
                    url = "${project.ext.scmUrl}"
                    connection = "${project.ext.scmConnection}"
                    developerConnection = "${project.ext.scmDeveloperConnection}"
                }
            }
        }
    }

    if (project.ext.isSigning()) {
        signing {
            sign publishing.publications
        }
    }

    // A version catalog need to publish Gradle metadata together to be correctly imported externally.
    if (!hasFlags('version-catalog')) {
        // For now, disable Gradle metadata generation until verifying it is correctly generated.
        tasks.withType(GenerateModuleMetadata) {
            enabled = false
        }
    }
}
