buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.1'
    }
}

apply plugin: 'com.google.protobuf'

managedDependencies {
    // gRPC
    [ 'grpc-core', 'grpc-stub', 'grpc-protobuf' ].each {
        compile "io.grpc:$it"
    }

    compile 'org.curioswitch.curiostack:protobuf-jackson'

    testCompile 'io.grpc:grpc-okhttp'
    testCompile 'io.grpc:grpc-testing'
}

def generatedFilesDir = "$projectDir/gen-src"

sourceSets {
    main {
        output.dir("${generatedFilesDir}/main/resources", builtBy: 'generateProto')
    }
    test {
        output.dir("${generatedFilesDir}/test/resources", builtBy: 'generateTestProto')
    }
}

protobuf {
    generatedFilesBaseDir = generatedFilesDir
    protoc {
        artifact = 'com.google.protobuf:protoc:3.2.0'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${rootProject.ext.dependencyManagement['io.grpc']['grpc-core'].version}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                option 'enable_deprecated=false'
            }
        }
        all().each { task ->
            task.generateDescriptorSet = true
            task.descriptorSetOptions.includeSourceInfo = true
            task.descriptorSetOptions.includeImports = true
            task.descriptorSetOptions.path =
                    "${generatedFilesDir}/${task.sourceSet.name}/resources/" +
                            "META-INF/armeria/grpc/armeria-${task.sourceSet.name}.dsc"
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java");
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc");
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/test/java");
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/test/grpc");
    }
}
