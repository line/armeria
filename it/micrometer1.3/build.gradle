dependencies {
    testImplementation project(':thrift0.15')
    testImplementation project(':grpc')
    testImplementation 'io.dropwizard.metrics:metrics-core'
    testImplementation 'io.prometheus:simpleclient_common'

    [ 'micrometer-core', 'micrometer-registry-prometheus' ].each {
        testImplementation("io.micrometer:$it") {
            version {
                // Will fail the build if the override doesn't work
                strictly '1.3.20'
            }
        }
    }
}

tasks.compileTestJava.source "${rootProject.projectDir}/core/src/test/java/com/linecorp/armeria/internal/common/metric/RequestMetricSupportTest.java",
        "${rootProject.projectDir}/thrift/src/test/java/com/linecorp/armeria/it/metric/PrometheusMetricsIntegrationTest.java",
        "${rootProject.projectDir}/thrift/src/test/java/com/linecorp/armeria/it/metric/DropwizardMetricsIntegrationTest.java",
        "${rootProject.projectDir}/grpc/src/test/java/com/linecorp/armeria/it/grpc/GrpcMetricsIntegrationTest.java"

task copyThriftFiles(type: Copy) {
    dependsOn project(':thrift0.15').tasks.compileTestThrift

    from "${rootProject.projectDir}/thrift/gen-src/test/java"
    into "${project.ext.genSrcDir}/test/java"
    include '**/HelloService.java'
}

task copyGrpcFiles(type: Copy) {
    dependsOn project(':grpc').tasks.generateTestProto

    from "${rootProject.projectDir}/grpc/gen-src/test/java", "${rootProject.projectDir}/grpc/gen-src/test/grpc"
    into "${project.ext.genSrcDir}/test/java"
}

tasks.compileTestJava.dependsOn(copyThriftFiles)
tasks.compileTestJava.dependsOn(copyGrpcFiles)
