dependencies {
    implementation project(':jetty11')
    // Dropwizard
    api libs.dropwizard4.core
    api libs.dropwizard4.util
    api libs.dropwizard4.lifecycle
    api libs.dropwizard4.jackson
    api libs.dropwizard4.validation
    api libs.dropwizard4.jersey
    api libs.dropwizard4.jetty

    // Dropwizard Metrics
    implementation libs.dropwizard.metrics.core

    implementation libs.javax.validation

    testImplementation libs.dropwizard4.testing
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-ADROPWIZARD_4']
}

// Do not relocate Guava because it's part of Dropwizard's public API.
[tasks.shadedJar, tasks.shadedTestJar].each { task ->
    task.relocators.clear()
    project.ext.relocations.each { Map<String, String> props ->
        def from = props['from']
        def to = props['to']
        if (from in ['com.google.common', 'com.google.thirdparty.publicsuffix']) {
            return
        }
        task.relocate from, to
    }
}
