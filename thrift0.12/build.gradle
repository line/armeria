// 0.10, 0.11, 0.12 and 0.13 are all ABI and API compatible with each other,
// so we do not publish artifacts like 'armeria-thrift0.10' but just run tests
// to make sure the classes generated by ':thrift' works with the classes
// compiled with an older version of libthrift.
//
// See also: ../thrift0.9/build.gradle

final def THRIFT_VERSION = '0.12.0'

dependencies {
    // Use the classes compiled with the latest libthrift,
    // but use an older version for test classes.
    testImplementation(project(':thrift')) {
        exclude group: 'org.apache.thrift', module: 'libthrift'
    }
    testImplementation("org.apache.thrift:libthrift:${THRIFT_VERSION}") {
        version {
            // Will fail the build if the override doesn't work
            strictly THRIFT_VERSION
        }
    }

    // Jetty, for testing TServlet interoperability.
    testImplementation 'org.eclipse.jetty:jetty-webapp'
    testImplementation 'org.eclipse.jetty.http2:http2-server'

    // Dropwizard and Prometheus, for testing metrics integration
    testImplementation 'io.dropwizard.metrics:metrics-core'
    testImplementation 'io.micrometer:micrometer-registry-prometheus'
    testImplementation 'io.prometheus:simpleclient_common'
}

// Use the test sources from ':thrift'.
// NB: We should never add these directories using the 'sourceSets' directive because that will make
//     them added to more than one project and having a source directory with more than one output directory
//     will confuse IDEs such as IntelliJ IDEA.
tasks.compileTestJava.source "${rootProject.projectDir}/thrift/src/test/java"
tasks.processTestResources.from "${rootProject.projectDir}/thrift/src/test/resources"

// Use the old compiler.
ext {
    thriftVersion = THRIFT_VERSION.substring(0, THRIFT_VERSION.lastIndexOf('.'));
}

// Disable checkstyle because it's checked by ':thrift'.
tasks.withType(Checkstyle) {
    onlyIf { false }
}
