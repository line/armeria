final def THRIFT_VERSION = '0.13.0'

dependencies {
    // Thrift
    api("org.apache.thrift:libthrift") {
        version {
            // Will fail the build if the override doesn't work
            strictly THRIFT_VERSION
        }
    }

    api 'javax.annotation:javax.annotation-api'

    // Jetty, for testing TServlet interoperability.
    testImplementation 'org.eclipse.jetty:jetty-webapp'
    testImplementation 'org.eclipse.jetty.http2:http2-server'

    // Dropwizard and Prometheus, for testing metrics integration
    testImplementation 'io.dropwizard.metrics:metrics-core'
    testImplementation 'io.micrometer:micrometer-registry-prometheus'
    testImplementation 'io.prometheus:simpleclient_common'
}

// Use the old compiler.
ext {
    thriftVersion = THRIFT_VERSION.substring(0, THRIFT_VERSION.lastIndexOf('.'));
}

// Keep the original Guava references in ThriftListenableFuture,
// which is the only place we expose Guava classes in our public API.
// NB: Keep this same with 'armeria-thrift0.9'.
tasks.shadedJar.exclude 'com/linecorp/armeria/common/thrift/ThriftListenableFuture*'
tasks.shadedJar.doLast {
    ant.jar(update: true, destfile: tasks.shadedJar.archivePath) {
        sourceSets.main.output.classesDirs.each { classesDir ->
            fileset(dir: "${classesDir}",
                    includes: 'com/linecorp/armeria/common/thrift/ThriftListenableFuture*')
        }
    }
}
