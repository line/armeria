dependencies {
    implementation(project(':grpc'))

    // switch to the protobuf-v4 version
    testImplementation platform(libs.protobuf4.bom)
    
    // copy from ../../grpc/build.gradle
    testImplementation(libs.gax.grpc) {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }
    testImplementation project(':prometheus1')
    testImplementation libs.grpc.okhttp
    testImplementation libs.grpc.testing
    testImplementation libs.micrometer.prometheus
    testImplementation libs.reactor.test
    testImplementation libs.reactivestreams.tck
}

def grpcDir = "${rootProject.projectDir}/grpc"

tasks.register('copyProtoFiles', Copy) {
    from "${rootProject.projectDir}/grpc/src/test/proto"
    into "${rootProject.projectDir}/it/grpc/protobuf4/gen-src/test/proto"
}

tasks.register('generateTestSources', Copy) {
    from("${grpcDir}/src/test/java")
    into "${project.ext.genSrcDir}/test/java"
    include "**/HttpJsonTranscoding*Test.java"
    include "**/HttpJsonTranscodingTestService.java"
    include "**/TestServiceImpl.java"
}

sourceSets {
    test {
        proto {
            srcDir "${project.ext.genSrcDir}/test/proto"
        }
    }
}

tasks.generateTestProto.dependsOn(tasks.copyProtoFiles)
tasks.compileTestJava.dependsOn(tasks.generateTestSources)
tasks.processTestResources.from "${grpcDir}/src/test/resources"
