import com.linecorp.armeria.conventions.spring.SpringBuildUtil

configurations {
    // TODO(hyangtack) The followings are not transitive dependencies and they are not in testRuntime
    //                 dependencies. Need to figure out why they are included in shadedJarTestRuntime dependencies.
    // Exclude jetty from shadedTest.
    shadedJarTestRuntime.exclude group: 'org.eclipse.jetty'
    shadedJarTestRuntime.exclude group: 'org.eclipse.jetty.http2'
}

dependencies {
    // To let a user choose between thrift and thrift0.9.
    if (project.ext.targetJavaVersion >= 11) {
        compileOnly project(':thrift0.18')
    } else {
        compileOnly project(':thrift0.17')
    }
    implementation project(':logback')

    optionalApi project(':prometheus1')
    optionalApi libs.micrometer.prometheus.legacy
    optionalApi libs.dropwizard.metrics.json
    api libs.javax.inject
    compileOnly libs.javax.validation
    api libs.spring.boot2.starter.webflux

    annotationProcessor libs.spring.boot2.configuration.processor

    testImplementation project(':spring:boot2-actuator-autoconfigure')
    if (project.ext.targetJavaVersion >= 11) {
        testImplementation project(':thrift0.18')
    } else {
        testImplementation project(':thrift0.17')
    }
    testImplementation libs.spring.boot2.starter.test
}

SpringBuildUtil.useMainSources(project(":spring:spring7"), project) {
    include '**/ArmeriaClientHttpRequest.java'
    include '**/ArmeriaClientHttpResponse.java'
    include '**/DataBufferFactoryWrapper.java'
}

SpringBuildUtil.useMainSources(project(":spring:boot4-webflux-autoconfigure"), project) {
    exclude '**/AbstractServerHttpResponseVersionSpecific.java'
    exclude '**/AbstractServerHttpRequestVersionSpecific.java'
    exclude '**/VersionSpecificConfiguration.java'
    exclude '**/VersionSpecificReactiveWebServerFactory.java'
    exclude '**/package-info.java'
    exclude '**/org.springframework.boot.autoconfigure.AutoConfiguration.imports'
    exclude '**/TlsUtil.java'
}

def boot4Webflux = "boot4-webflux-autoconfigure"
SpringBuildUtil.copyTestSources(project(":spring:${boot4Webflux}"), project) {
    include "**/*.java"
    filter { String line ->
        line.replaceAll(/\bjakarta\.annotation\./, 'javax.annotation.')
                .replaceAll(/\bjakarta\.validation\./, 'javax.validation.')
                .replaceAll(/\bjakarta\.persistence\./, 'javax.persistence.')
                .replaceAll(/\bjakarta\./, 'javax.')
    }
    exclude "**/EnableTestMetrics.java"
    exclude "**/SettableHealthIndicator.java"
    exclude "**/ObservationTest.java"
    exclude "**/VersionSpecificTestUtil.java"
}
