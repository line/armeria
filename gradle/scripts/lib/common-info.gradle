import java.time.LocalDateTime

rootProject.ext {
    projectName = project.findProperty('projectName')
    projectUrl = project.findProperty('projectUrl')
    projectDescription = project.findProperty('projectDescription')?: rootProject.name
    authorName = project.findProperty('authorName')?: 'LINE Corporation'
    authorEmail = project.findProperty('authorEmail')?: 'dl_oss_dev@linecorp.com'
    authorUrl = project.findProperty('authorUrl')?: 'https://engineering.linecorp.com/en/'
    inceptionYear = project.findProperty('inceptionYear')
    licenseName = project.findProperty('licenseName')
    licenseUrl = project.findProperty('licenseUrl')
    scmUrl = project.findProperty('scmUrl')
    scmConnection = project.findProperty('scmConnection')
    scmDeveloperConnection = project.findProperty('scmDeveloperConnection')
    copyrightFooter =
            '&copy; Copyright ' + "${rootProject.ext.inceptionYear}&ndash;${LocalDateTime.now().year} " +
            '<a href="' + rootProject.ext.authorUrl + '">' + rootProject.ext.authorName + '</a>. ' +
            'All rights reserved.'
    googleAnalyticsId = rootProject.findProperty('googleAnalyticsId')
}

subprojects {
    ext {
        projectName = rootProject.ext.projectName
        projectUrl = rootProject.ext.projectUrl
        projectDescription = rootProject.ext.projectDescription
        authorName = rootProject.ext.authorName
        authorEmail = rootProject.ext.authorEmail
        authorUrl = rootProject.ext.authorUrl
        inceptionYear = rootProject.ext.inceptionYear
        licenseName = rootProject.ext.licenseName
        licenseUrl = rootProject.ext.licenseUrl
        scmUrl = rootProject.ext.scmUrl
        scmConnection = rootProject.ext.scmConnection
        scmDeveloperConnection = rootProject.ext.scmDeveloperConnection
        copyrightFooter = rootProject.ext.copyrightFooter
    }
}

allprojects {
    ext {
        artifactId = {
            // Use the overridden one if available.
            def overriddenArtifactId = findOverridden('artifactIdOverrides', project)
            if (overriddenArtifactId != null) {
                return overriddenArtifactId
            }

            // Generate from the project names otherwise.
            List<Project> chain = []
            Project p = project
            for (;;) {
                chain += p
                p = p.parent
                if (p == null) {
                    break
                }
            }

            return String.join('-', chain.reverse().collect { it.name })
        }.call()
    }
}

// Check whether to enable automatic module names.
def isAutomaticModuleNameEnabled = 'true' == rootProject.findProperty('automaticModuleNames')

allprojects {
    ext {
        automaticModuleName = {
            if (!isAutomaticModuleNameEnabled) {
                return null
            }

            return project.provider {
                // per-project override
                if (project.ext.has('automaticModuleNameOverride')) {
                    def override = project.ext.get('automaticModuleNameOverride')
                    if (!(override instanceof String)) {
                        throw new IllegalStateException("project.ext.automaticModuleNameOverride must be a String: ${override}")
                    }
                    return override
                }

                // Use the overridden one if available.

                def overriddenAutomaticModuleName = findOverridden('automaticModuleNameOverrides', project)
                if (overriddenAutomaticModuleName != null) {
                    return overriddenAutomaticModuleName
                }

                // Generate from the groupId and artifactId otherwise.
                def groupIdComponents = String.valueOf(rootProject.group).split("\\.").toList()
                def artifactIdComponents =
                        String.valueOf(project.ext.artifactId).replace('-', '.').split("\\.").toList()
                def generatedName
                if (groupIdComponents.last() == artifactIdComponents.first()) {
                    generatedName = String.join('.', groupIdComponents + artifactIdComponents.drop(1))
                } else {
                    generatedName = String.join('.', groupIdComponents + artifactIdComponents)
                }
                generatedName = generatedName.replaceAll("\\.(\\d)", "_\$1")
                return generatedName
            }
        }.call()
    }
}

def findOverridden(String overridesPropertyName, Project project) {
    if (rootProject.ext.has(overridesPropertyName)) {
        def overrides = rootProject.ext.get(overridesPropertyName)
        if (!(overrides instanceof Map)) {
            throw new IllegalStateException("rootProject.ext.${overridesPropertyName} must be a Map: ${overrides}")
        }

        for (Map.Entry<String, String> e : overrides.entrySet()) {
            if (rootProject.project(e.key) == project) {
                return String.valueOf(e.value)
            }
        }
    }

    return null
}
