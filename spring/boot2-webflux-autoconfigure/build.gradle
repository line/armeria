configurations {
    // TODO(hyangtack) The followings are not transitive dependencies and they are not in testRuntime
    //                 dependencies. Need to figure out why they are included in shadedJarTestRuntime dependencies.
    // Exclude jetty from shadedTest.
    shadedJarTestRuntime.exclude group: 'org.eclipse.jetty'
    shadedJarTestRuntime.exclude group: 'org.eclipse.jetty.http2'
}

dependencies {
    // To let a user choose between thrift and thrift0.9.
    if (project.ext.targetJavaVersion >= 11) {
        compileOnly project(':thrift0.18')
    } else {
        compileOnly project(':thrift0.17')
    }
    implementation project(':logback')

    optionalApi project(':prometheus1')
    optionalApi libs.micrometer.prometheus.legacy
    optionalApi libs.dropwizard.metrics.json
    api libs.javax.inject
    compileOnly libs.javax.validation
    api libs.spring.boot2.starter.webflux

    annotationProcessor libs.spring.boot2.configuration.processor

    testImplementation project(':spring:boot2-actuator-autoconfigure')
    if (project.ext.targetJavaVersion >= 11) {
        testImplementation project(':thrift0.18')
    } else {
        testImplementation project(':thrift0.17')
    }
    testImplementation libs.spring.boot2.starter.test
}

// Copy the sources from ':spring:boot3-autoconfigure'.
task copyBoot3Sources(type: Copy) {
    from("${rootProject.projectDir}/spring/boot3-autoconfigure/src/main/java") {
        // Copy from /internal/spring package.
        include '**/ArmeriaConfigurationNetUtil.java'
        include '**/ArmeriaConfigurationSettingsUtil.java'
        include '**/ArmeriaConfigurationUtil.java'
        include '**/CustomAlias*KeyManager*.java'
        include '**/SpringDependencyInjector.java'
        include '**/ThriftServiceUtils.java'

        // Copy from /spring package.
        include '**/*ArmeriaBeanPostProcessor*.java'
        include '**/ArmeriaServerConfigurator.java'
        include '**/ArmeriaSettings.java'
        include '**/DocServiceConfigurator.java'
        include '**/DropwizardExpositionService.java'
        include '**/DropwizardSupport.java'
        include '**/HealthCheckServiceConfigurator.java'
        include '**/InternalServiceId.java'
        include '**/InternalServices.java'
        include '**/MetricCollectingServiceConfigurator.java'
        include '**/LocalArmeriaPort.java'
        include '**/LocalArmeriaPorts.java'
        include '**/PrometheusSupport.java'
        include '**/PrometheusLegacySupport.java'
        include '**/SpringDependencyInjector.java'
        include '**/Ssl.java'
    }

    into "${project.ext.genSrcDir}/main/java"
}

task copySpring6Sources(type: Copy) {
    from("${rootProject.projectDir}/spring/spring6/src/main/java") {
        include '**/ArmeriaClientHttpRequest.java'
        include '**/ArmeriaClientHttpResponse.java'
        include '**/DataBufferFactoryWrapper.java'
    }

    into "${project.ext.genSrcDir}/main/java"
}

// Copy the main and test sources from ':spring:boot3-webflux-autoconfigure'.
task generateSources(type: Copy) {
    dependsOn(tasks.copyBoot3Sources)
    dependsOn(tasks.copySpring6Sources)
    from("${"${rootProject.projectDir}/spring/boot3-webflux-autoconfigure"}/src") {
        exclude '**/AbstractServerHttpResponseVersionSpecific.java'
        exclude '**/AbstractServerHttpRequestVersionSpecific.java'
        exclude '**/package-info.java'
        exclude '**/org.springframework.boot.autoconfigure.AutoConfiguration.imports'
        exclude '**/TlsUtil.java'
    }

    into "${project.ext.genSrcDir}"
}

tasks.compileJava.dependsOn(tasks.generateSources)
tasks.processResources.dependsOn(tasks.generateSources)
tasks.processTestResources.dependsOn(tasks.generateSources)
