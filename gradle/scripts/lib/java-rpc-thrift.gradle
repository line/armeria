def libDir = "${buildscript.sourceFile.parentFile}"

configure(projectsWithFlags('java')) {
    def thriftJsonEnabled = true
    def fullCamel = false

    ext {
        thriftVersion = null
        thriftPath = null
        enableThriftFullCamel = { fullCamel = true }
        disableThriftJson = { thriftJsonEnabled = false }
    }

    // TODO(anuraaga): Consider replacing with https://github.com/yodle/griddle
    project.sourceSets.all { sourceSet ->
        def scope = sourceSet.name
        // Create the task assuming the source directory exists
        // so that subprojects can refer to the task.
        def task = project.tasks.create([
                name: sourceSet.getTaskName('compile', 'thrift'),
                group: 'Build',
                description: "Compiles the ${sourceSet.name} .thrift files.",
                type: CompileThriftTask.class,
        ]) as CompileThriftTask

        def sourcesJarTask = project.tasks.findByName(sourceSet.getTaskName('sources', 'jar'))
        if (sourcesJarTask) {
            sourcesJarTask.dependsOn(task)
        }

        // Use afterEvaluate to give subprojects a chance to override the properties.
        project.afterEvaluate {
            def srcDirs = project.findProperty(sourceSet.getTaskName('', 'thriftSrcDirs'))
            if (srcDirs == null) {
                def defaultSrcDir = "${projectDir}/src/${scope}/thrift"
                if (!project.file(defaultSrcDir).isDirectory()) {
                    // Disable the compile*Thrift task which turned out to be unnecessary.
                    task.enabled = false
                    return
                }
                srcDirs = [defaultSrcDir]
            }
            if (!(srcDirs instanceof Iterable) || srcDirs instanceof CharSequence) {
                srcDirs = [srcDirs]
            }
            srcDirs = project.files(srcDirs)

            def includeDirs = project.findProperty(sourceSet.getTaskName('', 'thriftIncludeDirs')) ?: []
            if (!(includeDirs instanceof Iterable) || includeDirs instanceof CharSequence) {
                includeDirs = [includeDirs]
            }
            includeDirs = project.files(includeDirs)
            def javaOutputDir = "${project.ext.genSrcDir}/${scope}/java"
            def jsonOutputDir = "${project.ext.genSrcDir}/${scope}/resources"
            task.configure {
                // configure caching
                task.thriftDirs.addAll(srcDirs)
                task.thriftDirs.addAll(includeDirs)
                task.thriftJsonEnabled = thriftJsonEnabled
                task.fullCamel = fullCamel
                outputs.dirs javaOutputDir, jsonOutputDir

                outputs.cacheIf { true }

                doFirst {
                    def actualThriftPath
                    if (project.findProperty('thriftPath') != null) {
                        actualThriftPath = project.findProperty('thriftPath')
                    } else {
                        actualThriftPath =
                                "${libDir}/thrift" +
                                "/${project.findProperty('thriftVersion')?: '0.18'}" +
                                "/thrift.${rootProject.osdetector.classifier}"
                    }

                    srcDirs.each { srcDir ->
                        project.fileTree(srcDir) {
                            include '**/*.thrift'
                        }.each { sourceFile ->
                            logger.info("Using ${actualThriftPath} to generate Java sources from ${sourceFile}")
                            project.mkdir(javaOutputDir)
                            def javaGenerator = 'java'
                            if (fullCamel) {
                                javaGenerator = 'java:fullcamel'
                            }
                            project.exec {
                                commandLine actualThriftPath
                                args '-gen', javaGenerator, '-out', javaOutputDir
                                includeDirs.each {
                                    args '-I', it
                                }
                                args sourceFile.absolutePath
                            }
                            if (thriftJsonEnabled) {
                                logger.info("Using ${actualThriftPath} to generate JSON from ${sourceFile}")
                                def outDir = "${jsonOutputDir}/META-INF/armeria/thrift"
                                project.mkdir(outDir)
                                project.exec {
                                    commandLine actualThriftPath
                                    args '-gen', 'json', '-out', outDir
                                    includeDirs.each {
                                        args '-I', it
                                    }
                                    args sourceFile.absolutePath
                                }
                            }
                        }
                    }
                }
            }

            def processResourcesTask = tasks.findByName(sourceSet.getTaskName('process', 'resources'))
            if (processResourcesTask != null) {
                processResourcesTask.dependsOn(task)
            }

            def compileTask = tasks.findByName(sourceSet.getCompileTaskName('java'))
            if (compileTask != null) {
                compileTask.dependsOn(task)
            }

            project.ext.getGenerateSourcesTask().dependsOn(task)
        }
    }
}

class CompileThriftTask extends DefaultTask {

    @InputFiles
    @PathSensitive(PathSensitivity.RELATIVE)
    def thriftDirs = new ArrayList<File>()

    @Input
    @Optional
    String thriftVersion = project.ext.thriftVersion

    @InputFile
    @PathSensitive(PathSensitivity.RELATIVE)
    @Optional
    File thriftBinary = project.ext.thriftPath != null ? project.file(project.ext.thriftPath) : null

    @Input
    def thriftJsonEnabled

    @Input
    def fullCamel
}
