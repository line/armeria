import com.linecorp.armeria.conventions.spring.SpringBuildUtil

dependencies {
    // To let a user choose between thrift and thrift0.9.
    if (project.ext.targetJavaVersion >= 11) {
        compileOnly project(':thrift0.18')
    } else {
        compileOnly project(':thrift0.17')
    }
    implementation project(':logback')

    optionalApi project(':prometheus1')
    optionalApi libs.micrometer.prometheus.legacy
    optionalApi libs.dropwizard.metrics.json

    api libs.javax.inject
    api libs.spring.boot2.autoconfigure
    compileOnly libs.javax.validation
    annotationProcessor libs.spring.boot2.configuration.processor

    testImplementation project(':grpc')

    if (project.ext.targetJavaVersion >= 11) {
        testImplementation project(':thrift0.18')
    } else {
        testImplementation project(':thrift0.17')
    }
    testImplementation libs.spring.boot2.starter
    testImplementation libs.spring.boot2.starter.actuator
    testImplementation libs.spring.boot2.starter.test
    // Enables cglib for testing
    testImplementation libs.spring5.aspects
}

def boot4Autoconfigure = "boot4-autoconfigure"

SpringBuildUtil.copyTestSources(project(":spring:boot4-autoconfigure"), project) {
    include "**/*.java"
    filter { String line ->
        line.replaceAll(/\bjakarta\./, 'javax.')
    }
}

// Use the sources from ':spring:boot4-autoconfigure'.
SpringBuildUtil.useMainSources(project(":spring:${boot4Autoconfigure}"), project)

// allows illegal access by cglib
if (project.ext.testJavaVersion >= 16) {
    tasks.withType(Test) {
        jvmArgs '--add-opens=java.base/java.lang=ALL-UNNAMED'
    }
}
