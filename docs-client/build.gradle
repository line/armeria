buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:${managedVersions['com.moowork.gradle:gradle-node-plugin']}"
    }
}

// Do not build if 'noWeb' property exists.
if (rootProject.hasProperty('noWeb')) {
    return
}

apply plugin: 'base'
apply plugin: 'com.moowork.node'

task buildWeb(type: YarnTask) {
    dependsOn 'yarn'

    args = ['run', 'build']

    inputs.dir('src')
    inputs.file('package.json')
    inputs.file('yarn.lock')
    outputs.dir('build/web')
}

task checkWeb(type: YarnTask) {
    dependsOn 'yarn'

    args = ['run', 'check']

    inputs.dir('src')
    inputs.file('package.json')
    inputs.file('yarn.lock')
    outputs.upToDateWhen { true }
}

task copyWeb(type: Copy) {
    dependsOn buildWeb

    from 'build/web'
    into 'build/javaweb/com/linecorp/armeria/server/docs'
}

tasks.assemble.dependsOn copyWeb
tasks.check.dependsOn checkWeb

node {
    version = '10.16.0'
    yarnVersion = '1.17.3'
    download = true
}
