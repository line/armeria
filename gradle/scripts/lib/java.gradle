// Enable checkstyle if the rule file exists.
def checkstyleConfigDir = "${rootProject.projectDir}/settings/checkstyle"
def checkstyleEnabled = new File(checkstyleConfigDir).isDirectory() &&
                        new File("${checkstyleConfigDir}/checkstyle.xml").isFile()

configure(rootProject) {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
}

configure(projectsWithFlags('java')) {

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    archivesBaseName = project.ext.artifactId

    // Delete the generated source directory on clean.
    ext {
        genSrcDir = "${projectDir}/gen-src"
    }
    clean {
        delete project.ext.genSrcDir
    }

    // Add the generated source directories to the source sets.
    project.sourceSets.all { sourceSet ->
        sourceSet.java.srcDir file("${project.ext.genSrcDir}/${sourceSet.name}/java")
        sourceSet.resources.srcDir file("${project.ext.genSrcDir}/${sourceSet.name}/resources")
    }

    // Set the sensible compiler options.
    tasks.withType(JavaCompile) {
        def task = delegate
        sourceCompatibility = project.findProperty('javaSourceCompatibility') ?: '1.8'
        targetCompatibility = project.findProperty('javaTargetCompatibility') ?: '1.8'
        options.encoding = 'UTF-8'
        options.warnings = false
        options.debug = true
        options.compilerArgs += '-parameters'
        project.configurations.each { Configuration cfg ->
            if (cfg.name == 'annotationProcessor' || cfg.name.endsWith('AnnotationProcessor')) {
                cfg.withDependencies { deps ->
                    // Use incremental compilation only when there are no annotation processors,
                    // because it seems to cause compilation errors for some processors such as Lombok.
                    def useIncrementalCompilation = deps.size() == 0
                    options.incremental = useIncrementalCompilation
                    logger.info("${useIncrementalCompilation ? 'Enabling' : 'Disabling'} " +
                                "incremental compilation for '${task.path}'")
                }
            }
        }
    }

    // Generate a source JAR.
    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    // Generate a javadoc JAR.
    task javadocJar(type: Jar, dependsOn: tasks.javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    // Generate source/javadoc JARs only when publishing.
    def requestedTaskNames = gradle.startParameter.taskRequests.inject([]) { a, b -> a + b.args }
    tasks.sourceJar.onlyIf {
        project.ext.isSigning() ||
        project.ext.isPublishing() ||
        requestedTaskNames.find { it =~ /(?:^|:)sourceJar$/ }
    }
    [tasks.javadoc, tasks.javadocJar].each {
        it.onlyIf {
            project.ext.isSigning() ||
            project.ext.isPublishing() ||
            requestedTaskNames.find { it =~ /(?:^|:)javadoc(?:Jar)?$/ }
        }
    }

    artifacts {
        archives jar
        archives sourceJar
        archives javadocJar
    }

    // Enable full exception logging for test failures.
    tasks.withType(Test) {
        testLogging.exceptionFormat = 'full'
    }

    // Use JUnit 5 if it's defined in the dependencies.
    if (managedVersions.containsKey('org.junit.jupiter:junit-jupiter-engine')) {
        tasks.withType(Test) {
            useJUnitPlatform()
        }
    }

    // Enforce checkstyle rules.
    if (checkstyleEnabled) {
        apply plugin: 'checkstyle'

        checkstyle {
            configFile = new File("${checkstyleConfigDir}/checkstyle.xml")
            configProperties = ['checkstyleConfigDir': "$checkstyleConfigDir"]
            if (managedVersions.containsKey('com.puppycrawl.tools:checkstyle')) {
                toolVersion = managedVersions['com.puppycrawl.tools:checkstyle']
            }
        }

        task checkstyle(group: 'Verification', description: 'Runs the checkstyle rules.') {
            project.sourceSets.all { SourceSet sourceSet ->
                def dependencyTask = project.tasks.findByName(sourceSet.getTaskName("checkstyle", ""))
                if (dependencyTask instanceof Checkstyle) {
                    dependsOn dependencyTask
                }
            }
        }

        test {
            dependsOn tasks.checkstyle
        }
    }
}
