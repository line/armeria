import com.linecorp.armeria.conventions.spring.SpringBuildUtil

configurations {
    // TODO(hyangtack) The followings are not transitive dependencies and they are not in testRuntime
    //                 dependencies. Need to figure out why they are included in shadedJarTestRuntime dependencies.
    // Exclude jetty from shadedTest.
    shadedJarTestRuntime.exclude group: 'org.eclipse.jetty'
    shadedJarTestRuntime.exclude group: 'org.eclipse.jetty.http2'
}

dependencies {
    // To let a user choose between thrift and thrift0.9.
    compileOnly project(':thrift0.18')
    implementation project(':logback')
    implementation project(':spring:spring7')

    api libs.spring.boot4.starter.webflux
    api libs.spring.boot4.webclient
    api libs.spring.boot4.web.server
    api libs.spring.boot4.actuator.autoconfigure
    api libs.jakarta.inject
    compileOnly libs.jakarta.validation
    // WebFlux uses `StandardWebSocketUpgradeStrategy`, which depends on Jakarta WebSocket API 2.1+, if no
    // custom `RequestUpgradeStrategy` is detected. We may remove jakarta.websocket dependencies if
    // Armeria WebFlux module supports WebSocket.
    // https://github.com/spring-projects/spring-framework/blob/main/spring-webflux/src/main/java/org/springframework/web/reactive/socket/server/support/HandshakeWebSocketService.java#L272-L291
    implementation libs.jakarta.websocket
    implementation libs.jakarta.websocket.client

    optionalApi project(':prometheus1')
    optionalApi libs.micrometer.prometheus.legacy
    optionalApi libs.dropwizard.metrics.json

    annotationProcessor libs.spring.boot4.configuration.processor

    testImplementation project(':spring:boot4-actuator-autoconfigure')
    testImplementation project(':thrift0.18')
    testImplementation libs.spring.boot4.starter.test
    testImplementation libs.spring.boot4.micrometer.observation
    testImplementation libs.spring7.web
    // Added for sharing test suites with boot2
    testImplementation libs.javax.inject
}

// Copy common files from boot4-autoconfigure module to gen-src directory in order to use them as a source set.
SpringBuildUtil.copyMainSources(project(":spring:boot4-autoconfigure"), project) {
    // Copy from /internal/spring package.
    include '**/AthenzSupport.java'
    include '**/ArmeriaConfigurationNetUtil.java'
    include '**/ArmeriaConfigurationSettingsUtil.java'
    include '**/ArmeriaConfigurationUtil.java'
    include '**/CustomAlias*KeyManager*.java'
    include '**/SpringDependencyInjector.java'
    include '**/ThriftServiceUtils.java'

    // Copy from /spring package.
    include '**/*ArmeriaBeanPostProcessor*.java'
    include '**/ArmeriaServerConfigurator.java'
    include '**/ArmeriaSettings.java'
    include '**/AthenzConfig.java'
    include '**/DocServiceConfigurator.java'
    include '**/DropwizardExpositionService.java'
    include '**/DropwizardSupport.java'
    include '**/HealthCheckServiceConfigurator.java'
    include '**/InternalServiceId.java'
    include '**/InternalServices.java'
    include '**/MetricCollectingServiceConfigurator.java'
    include '**/LocalArmeriaPort.java'
    include '**/LocalArmeriaPorts.java'
    include '**/PrometheusSupport.java'
    include '**/PrometheusLegacySupport.java'
    include '**/SpringDependencyInjector.java'
    include '**/Ssl.java'

    exclude '**/package-info.java'
}
