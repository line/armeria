import net.davidecavestro.gradle.jxr.JxrLog
import org.apache.maven.jxr.JXR

import java.time.LocalDateTime

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'kr.motd.gradle:sphinx-gradle-plugin:1.0.2.Final'
        classpath 'gradle.plugin.net.davidecavestro:gradle-jxr-plugin:0.2.1'
    }
}

apply plugin: 'kr.motd.sphinx'

ext.copyrightFooter =
        '&copy; Copyright ' + "${project.property('inceptionYear')}&ndash;${LocalDateTime.now().year} " +
        '<a href="http://linecorp.com/en/">LINE Corporation</a>. ' +
        'All rights reserved.'

sphinx {
    sourceDirectory "${project.projectDir}/src/sphinx"
}

task clean << {
    project.delete(project.buildDir)
}

task apidocs(type: Javadoc) {
    destinationDir = project.file("${project.buildDir}/site/apidocs")
    javaProjects.each { source it.sourceSets.main.java.srcDirs }
    classpath = javaProjects.inject(project.files()) { result, project ->
        result.from(project.sourceSets.main.runtimeClasspath)
    }

    options {
        def title = "Armeria ${project.version} API reference"
        docTitle = title
        windowTitle = title
        bottom = project.ext.copyrightFooter
        encoding = 'UTF-8'
        docEncoding = 'UTF-8'
        breakIterator = true
        version = false
        author = false
        keyWords = false
        quiet()
        stylesheetFile = project.file("${project.projectDir}/src/apidocs/stylesheet.css")

        // Enable all lints except the missing tag warnings
        addBooleanOption('Xdoclint:all').value = true
        addBooleanOption('Xdoclint:-missing').value = true

        // Exclude the machine-generate or internal-only classes
        exclude '**/Tomcat*ProtocolHandler.java'
        exclude '**/internal/**'
        exclude '**/thrift/v1/**'

        // Groups
        group 'Server', 'com.linecorp.armeria.server*'
        group 'Client', 'com.linecorp.armeria.client*'
        group 'Common', 'com.linecorp.armeria.common*'

        // External Javadoc links
        links 'https://netty.io/4.1/api',
              'https://developers.google.com/protocol-buffers/docs/reference/java',
              'https://people.apache.org/~thejas/thrift-0.9/javadoc/'
    }
}

task xref << {
    JXR jxr = new JXR ()
    jxr.dest = "${project.buildDir}/site/xref"
    jxr.inputEncoding = 'UTF-8'
    jxr.outputEncoding = 'UTF-8'
    jxr.log = new JxrLog(logger: logger)

    def sourceDirs = javaProjects.inject([]) { srcDirs, project ->
        project.sourceSets.main.java.srcDirs.each { srcDirs << it.path }
        return srcDirs
    }

    def title = "Armeria ${project.version} cross-reference"
    jxr.xref(sourceDirs, 'templates', title, title, project.ext.copyrightFooter)
    ant.copy(file: "${project.projectDir}/src/xref/stylesheet.css", todir: jxr.dest)
}

tasks.site {
    dependsOn xref
    dependsOn apidocs
}

task build {
    dependsOn site
}
