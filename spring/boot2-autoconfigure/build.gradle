dependencies {
    // To let a user choose between thrift and thrift0.9.
    if (project.ext.targetJavaVersion >= 11) {
        compileOnly project(':thrift0.18')
    } else {
        compileOnly project(':thrift0.17')
    }
    implementation project(':logback')

    optionalApi project(':prometheus1')
    optionalApi libs.micrometer.prometheus.legacy
    optionalApi libs.dropwizard.metrics.json

    api libs.javax.inject
    api libs.spring.boot2.autoconfigure
    compileOnly libs.javax.validation
    annotationProcessor libs.spring.boot2.configuration.processor

    testImplementation project(':grpc')

    if (project.ext.targetJavaVersion >= 11) {
        testImplementation project(':thrift0.18')
    } else {
        testImplementation project(':thrift0.17')
    }
    testImplementation libs.spring.boot2.starter
    testImplementation libs.spring.boot2.starter.actuator
    testImplementation libs.spring.boot2.starter.test
    // Enables cglib for testing
    testImplementation libs.hibernate.validator6
}

def boot3Autoconfigure = "boot3-autoconfigure"
def boot3AutoconfigureProjectDir = "${rootProject.projectDir}/spring/$boot3Autoconfigure"

// Use the sources from ':spring:boot3-autoconfigure'.
tasks.compileJava.dependsOn(project(":spring:$boot3Autoconfigure").tasks.compileJava)
tasks.compileTestJava.dependsOn(project(":spring:$boot3Autoconfigure").tasks.compileTestJava)

tasks.compileJava.source "${boot3AutoconfigureProjectDir}/src/main/java"
tasks.compileTestJava.source "${projectDir}/src/test/java",
        "${boot3AutoconfigureProjectDir}/src/test/java",
        "${boot3AutoconfigureProjectDir}/gen-src/test/grpc",
        "${boot3AutoconfigureProjectDir}/gen-src/test/java"
tasks.processTestResources.from "${boot3AutoconfigureProjectDir}/src/test/resources"
tasks.sourcesJar.from "${boot3AutoconfigureProjectDir}/src/main/java"
tasks.sourcesJar.from "${boot3AutoconfigureProjectDir}/src/main/resources"
tasks.javadoc.source "${boot3AutoconfigureProjectDir}/src/main/java"

// Disable checkstyle because it's checked by ':spring:boot3-autoconfigure'.
tasks.withType(Checkstyle) {
    onlyIf { false }
}

// allows illegal access by cglib
if (project.ext.testJavaVersion >= 16) {
    tasks.withType(Test) {
        jvmArgs '--add-opens=java.base/java.lang=ALL-UNNAMED'
    }
}
