dependencies {
    compile(project(':thrift')) {
        ext.optional = true // To let a user choose between thrift and thrift0.9.
    }
    compile project(':logback')

    compile 'io.micrometer:micrometer-spring-legacy'
    compile 'io.micrometer:micrometer-registry-prometheus'
    compile 'io.dropwizard.metrics:metrics-json'
    compile 'javax.inject:javax.inject'
    compileOnly 'javax.validation:validation-api'
    compile 'org.springframework.boot:spring-boot-starter:1.5.19.RELEASE'
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor:1.5.19.RELEASE'

    testCompile 'org.springframework.boot:spring-boot-starter-test:1.5.19.RELEASE'
}

// Use the sources from ':spring:boot-autoconfigure'.
// NB: We should never add these directories using the 'sourceSets' directive because that will make
//     them added to more than one project and having a source directory with more than one output directory
//     will confuse IDEs such as IntelliJ IDEA.
def autoconfigureProjectDir = "${rootProject.projectDir}/spring/boot-autoconfigure"
tasks.compileJava.source "${autoconfigureProjectDir}/src/main/java"
tasks.processResources.from "${autoconfigureProjectDir}/src/main/resources"
tasks.compileTestJava.source "${autoconfigureProjectDir}/src/test/java", "${autoconfigureProjectDir}/gen-src/test/java"
tasks.processTestResources.from "${autoconfigureProjectDir}/src/test/resources"
tasks.sourceJar.from "${autoconfigureProjectDir}/src/main/java"
tasks.sourceJar.from "${autoconfigureProjectDir}/src/main/resources"
tasks.javadoc.source "${autoconfigureProjectDir}/src/main/java"

tasks.compileTestJava.dependsOn(project(':spring:boot-autoconfigure').tasks.findByName('compileTestThrift'))

// Disable checkstyle because it's checked by ':spring:boot-autoconfigure'.
tasks.checkstyleMain.onlyIf { false }
tasks.checkstyleTest.onlyIf { false }
