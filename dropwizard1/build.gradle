final def DROPWIZARD_VERSION = '1.3.29'

dependencies {
    implementation project(':jetty9')
    // Dropwizard
    ['core', 'util', 'lifecycle', 'jackson', 'validation', 'jetty', 'jersey'].each {
        api("io.dropwizard:dropwizard-$it") {
            version {
                // Will fail the build if the override doesn't work
                strictly DROPWIZARD_VERSION
            }
        }
    }
    // Dropwizard Metrics
    implementation 'io.dropwizard.metrics:metrics-core'

    testImplementation('io.dropwizard:dropwizard-testing') {
        version {
            // Will fail the build if the override doesn't work
            strictly DROPWIZARD_VERSION
        }
    }
}

// Use the sources from ':dropwizard2'.
// NB: We should never add these directories using the 'sourceSets' directive because that will make
//     them added to more than one project and having a source directory with more than one output directory
//     will confuse IDEs such as IntelliJ IDEA.
tasks.compileJava.source "${rootProject.projectDir}/dropwizard2/src/main/java"
tasks.processResources.from "${rootProject.projectDir}/dropwizard2/src/main/resources"
tasks.compileTestJava.source "${rootProject.projectDir}/dropwizard2/src/test/java"
tasks.processTestResources.from "${rootProject.projectDir}/dropwizard2/src/test/resources"
tasks.sourcesJar.from "${rootProject.projectDir}/dropwizard2/src/main/java"
tasks.sourcesJar.from "${rootProject.projectDir}/dropwizard2/src/main/resources"
tasks.javadoc.source "${rootProject.projectDir}/dropwizard2/src/main/java"

// Do not relocate Guava because it's part of Dropwizard's public API.
[tasks.shadedJar, tasks.shadedTestJar].each { task ->
    task.relocators.clear()
    project.ext.relocations.each { Map<String, String> props ->
        def from = props['from']
        def to = props['to']
        if (from in ['com.google.common', 'com.google.thirdparty.publicsuffix']) {
            return
        }
        task.relocate from, to
    }
}

// Disable checkstyle because it's checked by ':dropwizard2'.
tasks.withType(Checkstyle) {
    onlyIf { false }
}
