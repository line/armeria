def isReleaseVersion = !(rootProject.version.endsWith('-SNAPSHOT'))
def publishSignatureRequired = ('true' == rootProject.findProperty('publishSignatureRequired'))
def publishUrlForRelease = rootProject.findProperty('publishUrlForRelease')
def publishUrlForSnapshot = rootProject.findProperty('publishUrlForSnapshot')
def publishUsernameProperty = rootProject.findProperty('publishUsernameProperty')
def publishPasswordProperty = rootProject.findProperty('publishPasswordProperty')

if (publishSignatureRequired) {
    apply plugin: 'signing'
}

rootProject.ext {
    isPublishing = { gradle.taskGraph.allTasks.find { it.name =~ /(?:^|:)publish[^:]*ToMaven[^:]*$/ } != null }
    if (publishSignatureRequired) {
        isSigning = {
            rootProject.signing.signatory != null &&
            (isReleaseVersion || rootProject.hasProperty('sign'))
        }
    } else {
        isSigning = { false }
    }

    if (publishSignatureRequired) {
        gradle.taskGraph.whenReady {
            // Do *NOT* proceed if a user is publishing a release version and signatory is missing.
            if (rootProject.ext.isPublishing() &&
                isReleaseVersion &&
                rootProject.signing.signatory == null) {

                throw new IllegalStateException(
                        'Cannot publish a release version without a GPG key; missing signatory')
            }
        }
    }
}

subprojects {
    ext {
        isPublishing = rootProject.ext.isPublishing
        isSigning = rootProject.ext.isSigning
    }
}


configure(projectsWithFlags('publish')) {
    apply plugin: 'maven-publish'

    if (project.ext.isSigning()) {
        apply plugin: 'signing'

        signing {
            required { true }
        }
    }

    publishing {
        repositories {
            maven {
                if (project.hasProperty('publishUrl')) {
                    url project.findProperty('publishUrl')
                } else if (isReleaseVersion) {
                    url publishUrlForRelease
                } else {
                    url publishUrlForSnapshot
                }

                credentials {
                    username = project.findProperty(publishUsernameProperty)
                    password = project.findProperty(publishPasswordProperty)
                }
            }
        }
    }

    task install(
            group: 'Publishing',
            description: 'An alias of publishToMavenLocal',
            dependsOn: tasks.publishToMavenLocal)
}
