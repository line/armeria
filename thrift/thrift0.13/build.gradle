dependencies {
    // Thrift
    api libs.thrift013

    api libs.javax.annotation

    // thrift api depends on httpclient4
    testImplementation libs.apache.httpclient4

    // Jetty, for testing TServlet interoperability.
    testImplementation libs.jetty94.webapp
    testImplementation libs.jetty94.http2.server

    // Dropwizard and Prometheus, for testing metrics integration
    testImplementation libs.dropwizard.metrics.core
    testImplementation libs.micrometer.prometheus
    testImplementation libs.prometheus

    // micrometer tracing
    testImplementation (libs.micrometer.tracing.integration.test) {
        exclude group: "org.mockito"
    }
    testImplementation libs.brave6.instrumentation.http.tests
}

// Use the old compiler.
def thriftFullVersion = libs.thrift013.get().versionConstraint.requiredVersion
ext {
    thriftVersion = thriftFullVersion.substring(0, thriftFullVersion.lastIndexOf('.'));
}

// Keep the original Guava references in ThriftListenableFuture,
// which is the only place we expose Guava classes in our public API.
// NB: Keep this same with 'armeria-thrift0.9'.
tasks.shadedJar.exclude 'com/linecorp/armeria/common/thrift/ThriftListenableFuture*'
tasks.shadedJar.doLast {
    ant.jar(update: true, destfile: tasks.shadedJar.archivePath) {
        sourceSets.main.output.classesDirs.each { classesDir ->
            fileset(dir: "${classesDir}",
                    includes: 'com/linecorp/armeria/common/thrift/ThriftListenableFuture*')
        }
    }
}
