def thriftVersion = ""
if (project.ext.targetJavaVersion >= 11) {
    thriftVersion = '0.18'
} else {
    thriftVersion = '0.17'
}

dependencies {
    testImplementation project(":thrift$thriftVersion")
    testImplementation project(':grpc')
    testImplementation libs.dropwizard.metrics.core
    testImplementation libs.prometheus

    testImplementation libs.micrometer13.core
    testImplementation libs.micrometer13.prometheus
}

tasks.compileTestJava.source "${rootProject.projectDir}/core/src/test/java/com/linecorp/armeria/internal/common/metric/RequestMetricSupportTest.java",
        "${rootProject.projectDir}/thrift/src/test/java/com/linecorp/armeria/it/metric/PrometheusMetricsIntegrationTest.java",
        "${rootProject.projectDir}/thrift/src/test/java/com/linecorp/armeria/it/metric/DropwizardMetricsIntegrationTest.java",
        "${rootProject.projectDir}/grpc/src/test/java/com/linecorp/armeria/it/grpc/GrpcMetricsIntegrationTest.java"

task copyThriftFiles(type: Copy) {
    dependsOn project(":thrift$thriftVersion").tasks.compileTestThrift
    dependsOn project(":thrift$thriftVersion").tasks.generateTestSources

    from "${rootProject.projectDir}/thrift/thrift$thriftVersion/gen-src/test/java"
    into "${project.ext.genSrcDir}/test/java"
    include '**/HelloService.java'
}

task copyGrpcFiles(type: Copy) {
    dependsOn project(':grpc').tasks.generateTestProto

    from "${rootProject.projectDir}/grpc/gen-src/test/java", "${rootProject.projectDir}/grpc/gen-src/test/grpc"
    into "${project.ext.genSrcDir}/test/java"
}

tasks.compileTestJava.dependsOn(copyThriftFiles)
tasks.compileTestJava.dependsOn(copyGrpcFiles)
