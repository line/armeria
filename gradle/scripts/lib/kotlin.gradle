configure(projectsWithFlags('kotlin')) {

    apply plugin: 'kotlin'
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    def target = "1.8"
    def compilerArgs = ["-Xjsr305=strict", "-java-parameters"]
    compileKotlin {
        kotlinOptions.jvmTarget = target
        kotlinOptions.freeCompilerArgs = compilerArgs
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = target
        kotlinOptions.freeCompilerArgs = compilerArgs
    }

    ktlint {
        // https://github.com/pinterest/ktlint/issues/764
        version.set("0.36.0")
        verbose.set(true)
        reporters {
            reporter "html"
        }
        // https://github.com/pinterest/ktlint/issues/527
        disabledRules = ["import-ordering"]

        filter {
            exclude("**/gen-src/**")
        }
    }

    // A workaround for 'runKtlintCheckOverMainSourceSet' uses this output of task ':generateProto' without
    // declaring an explicit or implicit dependency.
    tasks.runKtlintCheckOverMainSourceSet.dependsOn(project.ext.getGenerateSourcesTask())
}
