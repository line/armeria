final def SPRING_BOOT_VERSION = '2.1.10.RELEASE'
final def TOMCAT_VERSION = '8.5.49'

dependencyManagement {
    // Override Spring Boot and Tomcat versions.
    dependencies {
        dependencySet(group: 'org.springframework.boot', version: SPRING_BOOT_VERSION) {
            entry 'spring-boot-starter'
            entry 'spring-boot-starter-web'
            entry 'spring-boot-starter-test'
        }
        dependencySet(group: 'org.apache.tomcat.embed', version: TOMCAT_VERSION) {
            entry 'tomcat-embed-core'
            entry 'tomcat-embed-jasper'
            entry 'tomcat-embed-el'
        }
    }
}

dependencies {
    compile project(':spring:boot-starter')
    compile project(':tomcat')
    compile 'org.springframework.boot:spring-boot-starter'
    compile('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

// Use the sources from ':it:spring:boot-tomcat'.
// NB: We should never add these directories using the 'sourceSets' directive because that will make
//     them added to more than one project and having a source directory with more than one output directory
//     will confuse IDEs such as IntelliJ IDEA.
def tomcatProjectDir = "${rootProject.projectDir}/it/spring/boot-tomcat"
tasks.compileJava.source "${tomcatProjectDir}/src/main/java"
tasks.processResources.from "${tomcatProjectDir}/src/main/resources"
tasks.compileTestJava.source "${tomcatProjectDir}/src/test/java", "${tomcatProjectDir}/gen-src/test/java"
tasks.processTestResources.from "${tomcatProjectDir}/src/test/resources"
tasks.sourceJar.from "${tomcatProjectDir}/src/main/java"
tasks.sourceJar.from "${tomcatProjectDir}/src/main/resources"
tasks.javadoc.source "${tomcatProjectDir}/src/main/java"

// Disable checkstyle because it's checked by ':it:spring:boot-tomcat'.
tasks.withType(Checkstyle) {
    onlyIf { false }
}

// Make sure the correct Spring Boot and Tomcat versions are used.
afterEvaluate {
    [configurations.runtime, configurations.testRuntime].each { cfg ->
        cfg.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            def group = artifact.moduleVersion.id.group
            def version = artifact.moduleVersion.id.version
            switch (group) {
                case 'org.springframework.boot':
                    assert version == SPRING_BOOT_VERSION
                    break
                case 'org.apache.tomcat.embed':
                    assert version == TOMCAT_VERSION
                    break
            }
        }
    }
}
