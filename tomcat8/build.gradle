final def TOMCAT_VERSION = '8.5.69'

dependencies {
    // Tomcat
    api("org.apache.tomcat.embed:tomcat-embed-core") {
        version {
            // Will fail the build if the override doesn't work
            strictly TOMCAT_VERSION
        }
    }

    testImplementation("org.apache.tomcat.embed:tomcat-embed-jasper") {
        version {
            // Will fail the build if the override doesn't work
            strictly TOMCAT_VERSION
        }
    }

    // Replace commons-logging (Tomcat's logging API) with SLF4J.
    runtimeOnly 'org.slf4j:jcl-over-slf4j'
}

// Use the sources from ':tomcat9' as well as ours.
// NB: We should never add these directories using the 'sourceSets' directive because that will make
//     them added to more than one project and having a source directory with more than one output directory
//     will confuse IDEs such as IntelliJ IDEA.
tasks.compileJava.source "${rootProject.projectDir}/tomcat9/src/main/java"
tasks.compileJava.exclude '**/ConfigFileLoaderInitializer*'
tasks.processResources.from "${rootProject.projectDir}/tomcat9/src/main/resources"
tasks.compileTestJava.source "${rootProject.projectDir}/tomcat9/src/test/java"
tasks.processTestResources.from "${rootProject.projectDir}/tomcat9/src/test/resources"
tasks.sourcesJar.from "${rootProject.projectDir}/tomcat9/src/main/java"
tasks.sourcesJar.from "${rootProject.projectDir}/tomcat9/src/main/resources"
tasks.sourcesJar.exclude '**/ConfigFileLoaderInitializer*'
tasks.javadoc.source "${rootProject.projectDir}/tomcat9/src/main/java"
tasks.javadoc.exclude '**/ConfigFileLoaderInitializer*'
