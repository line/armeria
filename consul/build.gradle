import static org.gradle.internal.os.OperatingSystem.current

import com.pszymczyk.consul.BinariesManager

def consulVersion = '1.11.3'
def consulBinaryDownloadDir = new File(gradle.gradleUserHomeDir, "caches/embedded-consul/${consulVersion}")

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.pszymczyk.consul:embedded-consul:${managedVersions['com.pszymczyk.consul:embedded-consul']}"
    }
}

dependencies {
    // Embedded Consul
    testImplementation 'com.pszymczyk.consul:embedded-consul'

    // Embedded Consul removed Groovy from transitive dependencies
    // https://github.com/pszymczyk/embedded-consul/pull/102
    testImplementation 'org.codehaus.groovy:groovy-xml'
}

task consulBinary {
    inputs.property('consulVersion', consulVersion)
    inputs.property('consulBinaryDownloadDir', consulBinaryDownloadDir)

    def consulBinaryFile = new File(consulBinaryDownloadDir, current().isWindows() ? 'consul.exe' : 'consul')
    outputs.file(consulBinaryFile)

    doLast {
        def binariesManager = new BinariesManager(consulBinaryDownloadDir.toPath(), consulVersion)
        if (!binariesManager.isBinaryDownloaded()) {
            // Download the binary.
            logger.lifecycle("Downloading the Consul ${consulVersion} into ${consulBinaryDownloadDir} ..")
            consulBinaryDownloadDir.mkdirs()
            binariesManager.ensureConsulBinaries()

            // Make sure the binary has the correct permission so that Gradle can hash it.
            ant.chmod(perm: 'u+rwx,a+rx', file: consulBinaryFile)
        }
    }
}

tasks.withType(Test) {
    dependsOn tasks.consulBinary
    environment('CONSUL_VERSION', consulVersion)
    environment('CONSUL_BINARY_DOWNLOAD_DIR', consulBinaryDownloadDir)
}
