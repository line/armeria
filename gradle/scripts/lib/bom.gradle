configure(projectsWithFlags('bom')) {
    apply plugin: 'java-platform'
    apply plugin: 'maven-publish'

    publishing {
        publications {
            bom(MavenPublication) {
                from components.javaPlatform

                pom.packaging = 'pom'
            }
        }
    }

    dependencies {
        constraints {
            afterEvaluate {
                def bomGroups, subs
                if (rootProject.ext.has('bomGroups')) {
                    bomGroups = rootProject.ext.bomGroups
                    if (!(bomGroups instanceof Map)) {
                        throw new IllegalStateException("bomGroups must be a Map: ${bomGroups}")
                    }
                    if (!bomGroups.containsKey(project.path)) {
                        logger.warn("{} won't be included to bomGroups property. If not intended, " +
                                    "Please check bomGroups property", project.name)
                    }
                    subs = bomGroups.get(project.path)
                    if (!(subs instanceof List)) {
                        throw new IllegalStateException("bomGroups' value must be a List: ${subs}")
                    }
                }

                projectsWithFlags('java', 'publish').toList().sort { a, b ->
                    def groupComparison = "${a.group}".compareTo("${b.group}")
                    if (groupComparison != 0) {
                        return groupComparison
                    }
                    return "${a.ext.artifactId}".compareTo("${b.ext.artifactId}")
                }.each { p ->
                    if (rootProject.ext.has('bomGroups')) {
                        if (!bomGroups.containsKey(project.path)) {
                            api "${p.group}:${p.ext.artifactId}:${p.version}"
                        } else {
                            if (subs.contains(p.path)) {
                                api "${p.group}:${p.ext.artifactId}:${p.version}"
                            } else if (p.path.startsWith(project.path)) {
                                logger.warn("{} won't be included in {} bom. If not intended, " +
                                            "Please check bomGroups property", p.name, project.name)
                            }
                        }
                    } else {
                        api "${p.group}:${p.ext.artifactId}:${p.version}"
                    }
                }
            }
        }
    }

    tasks.assemble.dependsOn {
        tasks.generatePomFileForBomPublication
    }
}
