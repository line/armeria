configurations {
    // TODO(hyangtack) The followings are not transitive dependencies and they are not in testRuntime
    //                 dependencies. Need to figure out why they are included in shadedTestRuntime dependencies.
    // Exclude jetty from shadedTest.
    shadedTestRuntime.exclude group: 'org.eclipse.jetty'
    shadedTestRuntime.exclude group: 'org.eclipse.jetty.http2'
}

dependencies {
    // To let a user choose between thrift and thrift0.9.
    compileOnly project(':thrift0.17')
    implementation project(':logback')

    optionalApi libs.micrometer.prometheus
    optionalApi libs.dropwizard.metrics.json
    api libs.javax.inject
    compileOnly libs.javax.validation
    api libs.spring.boot2.starter.webflux

    annotationProcessor libs.spring.boot2.configuration.processor

    testImplementation project(':spring:boot2-actuator-autoconfigure')
    testImplementation project(':thrift0.17')
    testImplementation libs.spring.boot2.starter.test
}

// Use the sources from ':spring:boot3-webflux-autoconfigure'.
def boot3WebFlux = "boot3-webflux-autoconfigure"
def boot3WebFluxProjectDir = "${rootProject.projectDir}/spring/$boot3WebFlux"
tasks.compileJava.dependsOn(project(":spring:$boot3WebFlux").tasks.compileJava)
tasks.compileTestJava.dependsOn(project(":spring:$boot3WebFlux").tasks.compileTestJava)

tasks.compileJava.source "${boot3WebFluxProjectDir}/src/main/java",
        "${boot3WebFluxProjectDir}/gen-src/main/java"
tasks.processResources.from "${boot3WebFluxProjectDir}/src/main/resources"

tasks.sourcesJar.from "${boot3WebFluxProjectDir}/src/main/java", "${boot3WebFluxProjectDir}/gen-src/main/java"
tasks.sourcesJar.from "${boot3WebFluxProjectDir}/src/main/resources"
tasks.javadoc.source "${boot3WebFluxProjectDir}/src/main/java", "${boot3WebFluxProjectDir}/gen-src/main/java"

// Copy test files from boot3-autoconfigure module to gen-src directory in order to use them as
// a test set.
task generateSources(type: Copy) {
    from "${boot3WebFluxProjectDir}/src/test"
    into "${project.ext.genSrcDir}/test"
    // ByteBufLeakTest could be flaky if run in parallel.
    exclude '**/ByteBufLeakTest.java'
}
